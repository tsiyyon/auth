plugins {
    id "java"
    id "idea"
    id "maven"
    id "signing"
}

group = 'me.tzion'
archivesBaseName = "identity"
version = '0.0.1'

ext {
    jaxrs = '2.1'
    junit = '4.12'
    guice = '4.1.0'
    jersey = '2.26'
    mockito = '1.10.19'
    hamcrest = '1.3'
}

repositories {
    jcenter()
    mavenLocal()
}


[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
    compile "javax.ws.rs:javax.ws.rs-api:$jaxrs"
    compile "com.google.inject:guice:$guice"
    compile "org.glassfish.jersey.core:jersey-server:$jersey"
    compile "org.glassfish.jersey.core:jersey-common:$jersey"
    compile "org.glassfish.jersey.inject:jersey-hk2:$jersey"

    testCompile "junit:junit:$junit"
    testCompile "org.hamcrest:hamcrest-all:$hamcrest"
    testCompile "org.mockito:mockito-all:$mockito"

    testCompile "org.glassfish.jersey.test-framework:jersey-test-framework-core:$jersey"
    testCompile "org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:$jersey"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'identity'
                packaging 'jar'
                description 'The jersey identity module'

                scm {
                    connection 'scm:git:git://github.com/tsiyyon/identity.git'
                    developerConnection 'scm:git:git://github.com/tsiyyon/identity.git'
                    url 'https://github.com/tsiyyon/identity'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'sjkyspa'
                        name 'morpheus'
                        email 'sjkyspa@gmail.com'
                    }
                }
            }
        }
    }
}